#version 450
#extension GL_EXT_mesh_shader : require

const uint cShaderGroupSize = 128;
const uint cMaxVerticesPerMeshlet = 256;
const uint cMaxPrimitivesPerMeshlet = 256;

layout(local_size_x=cShaderGroupSize, local_size_y=1, local_size_z=1) in;
layout(triangles) out;
layout(max_vertices=cMaxVerticesPerMeshlet, max_primitives=cMaxPrimitivesPerMeshlet) out;

struct Meshlet {
  uint vertexCount;
  uint vertexOffset;
  uint primitiveCount;
  uint primitiveOffest;
};

struct Vertex {
  vec3 position;
  vec3 color;
  vec2 texCoord;
  vec3 normal;
  vec3 tangent;
  vec3 bitangent;
};

layout(push_constant) uniform PerMeshPCIn
{
  mat4x4 meshToModel;
} perMesh;

layout(binding=0, std140, row_major) uniform CameraUBOIn {
  mat4 worldToClip;
  mat4 worldToView;
  mat4 viewToClip;
  mat4 clipToView;
  mat4 viewToWorld;
  mat4 clipToWorld;
} camera;

layout(binding=1, std140) readonly buffer MeshletsSSBOIn {
  Meshlet meshlets[];
};

layout(binding=2, std140) readonly buffer VerticesSSBOIn {
  Vertex vertices[];
};

layout(binding=3) readonly buffer PrimitiveIndicesSSBOIn {
  uint primitiveIndices[];
};

layout(binding=4) readonly buffer VertexIndicesSSBOIn {
  uint vertexIndices[];
};

layout(location=0) out PerVertexData {
  vec3 color;
  vec2 texCoord;
} v_out[];

void main()
{
  const uint groupIndex = gl_WorkGroupID.x;
  const uint groupThreadIndex = gl_LocalInvocationID.x;

  const Meshlet meshlet = meshlets[groupIndex];
  
  if (groupThreadIndex < meshlet.vertexCount)
  {
    // vertexIndices ???
    const uint vertexIndex = groupThreadIndex + meshlet.vertexOffset;
    
    gl_MeshVerticesEXT[groupThreadIndex].gl_Position = vec4(vertices[vertexIndex].position, 1.0) * perMesh.meshToModel * camera.worldToView * camera.viewToClip;
    v_out[groupThreadIndex].color = vertices[vertexIndex].color;
    v_out[groupThreadIndex].texCoord = vertices[vertexIndex].texCoord;
  }

  if (groupThreadIndex < meshlet.primitiveCount)
  {
    const uint packedIndices = primitiveIndices[groupThreadIndex + meshlet.primitiveOffest];
    gl_PrimitiveTriangleIndicesEXT[groupThreadIndex] = uvec3(
      packedIndices & 0x3ff, 
      (packedIndices >> 10) & 0x3ff, 
      (packedIndices >> 20) & 0x3ff);
  }

  SetMeshOutputsEXT(meshlet.vertexCount, meshlet.primitiveCount);
}
